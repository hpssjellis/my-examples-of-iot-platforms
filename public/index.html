<h6>Version 1.0.4-26</h6>


<h1 align=center > Which IOT cloud platform should my students use with the powerful Arduino PortentaH7!</h1>

<div align=center> So I researched very quickly 90 IOT Cloud platforms. And looked deeper into about 15 platforms.</div>

<h1 Align=center>And the winner is:   <a href="https://io.adafruit.com/">Adafruit.io</a></h1>

<h3  align=center >Here is an image of my criteria for my students:<br>
<img src="media/iot-needs.jpg"  width=300   style="border-style:groove; border-color:red; border-width:10px;  " /><br>
</h3>


<h3 Align=center>Why Adafruit?</h3>
<ul>
<li> Great MQTT, Connects well with Helium, uses CayenneLPP for easy JSON data compression. Works well with Arduino Products.
<li> Has HTTP(s) access to online variables (I will check if you can download data using HTTPS)
<li> Awesome Developers, when asked if they could activate MQTT-Websockets they had it working in one day?
<li> MQTT-Websockets working
<li> Has a free tier with a sensible amount of data etc. 
</ul> 
  

<h3><a href="#steps">Steps for students</a> to use Adafruit.io</h3>
<ol>  
<br><li> Signup/Login to the <a href="https://accounts.adafruit.com/users/sign_up/">Adafruit.io</a> website.Click "IO" --> Click "MY-KEY" --> Copy your <b>Username</b> and your <b>Active Key</b> 
<br><li> Then load this page to send/receive data from a static MQTT-Websocket  <a href="ada-websocket02.html"> ada-websocket02.html </a> IT WORKS GREAT!
  <br><li> Now try an MQTT app for Android like <a href="https://play.google.com/store/apps/details?id=snr.lab.iotmqttpanel.prod&hl=en_CA&gl=US">iotmqttpanel</a> 
    or for any laptop <a href="https://nodered.org/">Node-Red</a> or for Windows try <a href="http://mqtt-explorer.com/">MQTT-explorer</a> <br>
    Reminder to use "feeds" and not "feed"  <b>/{username}/feeds/{name-of-feed} </b><br>
    If you find these confusing then just move on

<br><li> Send data from a static webpage over  HTTP 
  <ol> The following needs your webhook key. That's it.
  
   <li>Easiest way to send data to io.adafruit.com; Just use a normal HTML form. Copy following into a file with a .html extension<br> 
     <textarea rows=3 cols=80 NOWRAP>
Adafruit webhooks are in the format<br> 
https://io.adafruit.com/api/v2/webhooks/feed/rvVztyR9G5D51ix6xoNkFCMMGG9T
<form id="myForm01"  target="_blank"  action="https://io.adafruit.com/api/v2/webhooks/feed/rvVztyR9G5D51ix6xoNkFCMMGG9T" method="POST">
  Send your choice:
  <select name="value">
    <option value="0" selected>0</option>
    <option value="1">1</option>
    <option value="2">2</option>
  </select><br>
  <input type="submit" value="Send data to io.adafruit.com">
</form> 
    </textarea>
  <br> <li><a href="ada-write-to-feed01.html">ada-write-to-feed01.html</a> Send raw data to Adafruit using vanilla Javascript async await fetch. WORKS GREAT
   <br><li><a href="ada-write-to-feed02.html">ada-write-to-feed02.html</a> Send formatted <b>value=4</b> data to Adafruit using vanilla Javascript async await fetch. WORKS GREAT
   <br><li><a href="ada-write-to-feed03.html">ada-write-to-feed03.html</a> Send JSON formatted <b>{"value":"4"}</b> data to Adafruit using vanilla Javascript async await fetch. WORKS GREAT
  
  </ol>
    
<li> Read data from a webpage over  HTTP   Draft: <a href="ada-read-feed02.html">ada-read-feed02.html</a>  WORKS GREAT <br><br>
  
  <H3>Now the important Arduino Connections</h3>
  <br><li> Send data from an Arduino PortentaH7 with WiFi using form style HTTP POST action <a href="arduino-portenta-post01.ino">arduino-portenta-post01.ino</a><br>
  Code here as well<br>
<textarea rows=3 cols=80 NOWRAP>
 /*By rocksetta
 * Use at your own risk
 * 
*/

#include <Arduino.h> // Only needed by https://platformio.org/
#include "WiFi.h"
#include "WiFiSSLClient.h"


///////please enter your sensitive data in the Secret tab/arduino_secrets.h

// Choose either the following arduino_secrets.h tab and bracket out the next 2 lines after it
// That route is more secure.
// Or just change the lines below for your Network and Password. Eaier but not as secure
// Since if you share this file it will have your info on it


////////////////////////////// Set the below values /////////////////////////////////////////////


//#include "arduino_secrets.h"   // more safe
#define SECRET_SSID "yourSSID"
#define SECRET_PASS "yourPASS"


// IPAddress server(52.54.163.195);  // IP to connect to - use either
char server[] = "io.adafruit.com";    // Server to connect 

// note the slight difference between a webhook and what we need
// https://io.adafruit.com/api/v2/webhooks/feed/rvVztyR9G5D51ix6xoNkFCMMGG9T

String myAdafruitWebhook = "/api/v2/webhooks/feed/rvVztyR9G5D51ix6xoNkFCMMGG9T";
String myData = "value=234";


/////////////////////////////// set the above ////////////////////////////////////////


char ssid[] = SECRET_SSID;        // your network SSID (name)
char pass[] = SECRET_PASS;        // your network password (use for WPA, or use as key for WEP)
int keyIndex = 0;                 // your network key index number (needed only for WEP)

int status = WL_IDLE_STATUS;

WiFiSSLClient client;

void setup() {

  Serial.begin(115200);
  //while (!Serial) {} // wait for serial port to connect. I DON'T LIKE WAITING
  
  // Below non-blocking and gives time for serial monitor
  digitalWrite(LEDB, LOW);  
  delay(5000);
  Serial.println("Wait a bit to connect serial monitor");
  digitalWrite(LEDB, HIGH);  
  delay(5000);
  Serial.println("Wait a bit");
  digitalWrite(LEDB, LOW);  
  delay(5000);
  Serial.println("Wait a bit");
  
  pinMode(LED_BUILTIN, OUTPUT);      
  pinMode(LEDB, OUTPUT);     
  digitalWrite(LEDB, LOW);          // blue on while connecting
  digitalWrite(LED_BUILTIN, HIGH);          

  // check for the WiFi module:
  if (WiFi.status() == WL_NO_MODULE) {
    Serial.println("Communication with WiFi module failed!");
    // don't continue
    while (true);
  }

  // attempt to connect to WiFi network:
  while (status != WL_CONNECTED) {
    Serial.print("Attempting to connect to SSID: ");
    Serial.println(ssid);
    // Connect to WPA/WPA2 network. Change this line if using open or WEP network:
    status = WiFi.begin(ssid, pass);

    // wait 10 seconds for connection:
    delay(10000);
  }
  Serial.println("Connected to WiFi");
  digitalWrite(LEDB, HIGH);          
  digitalWrite(LED_BUILTIN, LOW);    // Green on When Connected
  
  // print the SSID of the network you're attached to:
  Serial.print("SSID: ");
  Serial.println(WiFi.SSID());

  // print your board's IP address:
  IPAddress ip = WiFi.localIP();
  Serial.print("IP Address: ");
  Serial.println(ip);

  // print the received signal strength:
  long rssi = WiFi.RSSI();
  Serial.print("signal strength (RSSI):");
  Serial.print(rssi);
  Serial.println(" dBm");

  Serial.println("\nStarting connection to server...");
  // if you get a connection, report back via serial:
  if (client.connect(server, 443)) {

     Serial.println("Conected to Server! Sending data");

     client.println("POST " + myAdafruitWebhook + " HTTP/1.1");
     client.println("connection: close");
     client.println("content-type: application/x-www-form-urlencoded;charset=utf-8");   //application/x-www-form-urlencoded   application/json
     client.println("host: io.adafruit.com");
     client.println("user-agent: arduino");
     client.println("content-length: " + String(myData.length()) );
     client.println();
     client.println(myData);   
     client.println();
     client.println();

  }
}

void loop() {
  // if there are incoming bytes available
  // from the server, read them and print them:
  while (client.available()) {
    char c = client.read();
    Serial.write(c);
  }

  // if the server's disconnected, stop the client:
  if (!client.connected()) {
    Serial.println();
    Serial.println("disconnecting from server.");
    client.stop();

    // do nothing forevermore:
    while (true);
  }
}
        
         
</textarea>  
  
  <br><li> Send data from an Arduino PortentaH7 using MQTT over WiFi <a href="arduino-portenta-mqtt-client01.ino">arduino-portenta-mqtt-client01.ino</a>
    Code here as well<br>
<textarea rows=3 cols=80 NOWRAP>
/*
 * Uses the library MQTT.h from https://github.com/256dpi/arduino-mqtt 
 * but made for the PortentaH7
 * By Rocksetta
 * Use at your own risk!
*/

#include <Arduino.h> // Only needed by https://platformio.org/
#include "WiFi.h"
#include "WiFiSSLClient.h"

#include <MQTT.h>

////////////////////////////////// You need to enter these ///////////////////////////////////////////////

//#include "arduino_secrets.h"   // more safe to move the following
#define SECRET_SSID "yourSSID"
#define SECRET_PASS "yourPASS"

#define THE_SERVER "io.adafruit.com"
int myPort = 8883;   //ssl 8883,  insecure 1883


#define THE_RANDOM_ID "random2345345"   // if using several devices each one needs this to be unique
#define THE_USER_NAME "yourUSER"
#define THE_ADAFRUIT_ACTIVE_KEY "aio_aaaaa-your-active-key-aaaaaa"
#define THE_TOPIC_USER_FEEDS_KEY "yourUSER/feeds/yourFeedKey"   // can be different for subscribe and publish

long myMillisSecondDelay = 30000;   // 30000 = 30 seconds



////////////////////////////// End the entry area //////////////////////////////////////////////////////

int status = WL_IDLE_STATUS;


char ssid[] = SECRET_SSID;        // your network SSID (name)
char pass[] = SECRET_PASS;        // your network password (use for WPA, or use as key for WEP)

WiFiSSLClient netSSL;
MQTTClient client;

unsigned long lastMillis = 0;

void connect() {
  Serial.print("Checking wifi to: " + String(ssid));
  while (status != WL_CONNECTED) {
    Serial.print(".");
    // Connect to WPA/WPA2 network. Change this line if using open or WEP network:
    status = WiFi.begin(ssid, pass);

    // wait 10 seconds for connection:
    delay(3000);
  }

  Serial.print("\nWifi Connected, now connecting to the MQTT Broker ...");
                         // clientID (a random unique number), username, activeKey, bool for skip
 // while (!client.connect("arduino8234545", "jerteach", "aio_EHAu48LWqMZ843WhhXoFYCSnKBcp", false)) {
  while (!client.connect(THE_RANDOM_ID, THE_USER_NAME, THE_ADAFRUIT_ACTIVE_KEY)) {
    Serial.print(".");
    delay(3000);
  }

  Serial.println("\nconnected to : " + String(THE_SERVER) );

  client.subscribe(THE_TOPIC_USER_FEEDS_KEY);
  // client.unsubscribe(THE_TOPIC_USER_FEEDS_KEY);
}

void messageReceived(String &topic, String &payload) {
  Serial.println("incoming: " + topic + " : " + payload);

  // Note: Do not use the variable "client" here as it will lock up
}

void setup() {
  
  Serial.begin(115200);
  //while (!Serial) {} // wait for serial port to connect. I DON'T LIKE WAITING
 
   pinMode(LED_BUILTIN, OUTPUT);      
  pinMode(LEDB, OUTPUT);   
  digitalWrite(LEDB, LOW);  
  delay(5000);
  Serial.println("Wait a bit to connect serial monitor");
  digitalWrite(LEDB, HIGH);  
  delay(5000);
  Serial.println("Wait a bit");
  digitalWrite(LEDB, LOW);  
  delay(5000);
  Serial.println("Wait a bit");
  
  
  digitalWrite(LEDB, LOW);          // blue on while connecting
  digitalWrite(LED_BUILTIN, HIGH);          

  // check for the WiFi module:
  if (WiFi.status() == WL_NO_MODULE) {
    Serial.println("Communication with WiFi module failed!");
    // don't continue
    while (true);
  }
  
  // MQTT brokers usually use port 8883 for secure connections.
  client.begin(THE_SERVER, myPort, netSSL);
  client.onMessage(messageReceived);

  connect();
  digitalWrite(LEDB, HIGH);          // Green on while connected
  digitalWrite(LED_BUILTIN, LOW); 
}

void loop() {
  client.loop();

  if (!client.connected()) {
    connect();
  }

  // publish a message roughly every 30 seconds.
  if (millis() - lastMillis > myMillisSecondDelay) {
    lastMillis = millis();
    
    // Send whatever reading is at A0 from 0->1023 even if nothing is connected to it
    client.publish(THE_TOPIC_USER_FEEDS_KEY, String(analogRead(A0)) );
  }
}  
  
  
</textarea>    
 <br> <li> Send data from an Arduino  PortentaH7 using Websockets over WiFi or Ethernet  (Working on this)
  
  <br><li>Try an Adafruit MQTT integration with the <a href="https://console.helium.com/">Helium console</a> (advanced: need a LoRaWan board like the Portenta LoRa Vision Shield) Get 10,000 Data Credits for signing up. WORKS GREAT
   
  <br><li>Try an Adafruit HTTP integration with the <a href="https://console.helium.com/">Helium console</a> (advanced:see above). I still need to test this.
</ol>  
  
 <br><br><br> <hr><br><br><br>
  
  
  
  

<h2 Align=center>Second place is:   <a href="https://hiveMQ.com/">HiveMQ</a></h2>
For giving up to 100 free devices and supporting Websocket-MQTT and for the potential connections with small devices



<h2 Align=center>Third place is:   <a href="https://docs.arduino.cc/cloud/iot-cloud">Arduino Cloud</a></h2>
For making things easy


<h2 Align=center>Honourable Mention to: </h2>

<br><br><a href="https://ubidots.com/">Ubidots.com</a> 
<br><br><a href="https://tago.io/">Tago.io</a> 
<br><br><a href="https://www.qubitro.com/">Qubitro:</a>
<br><br><a href="https://thingsboard.io/">Thingsboard:</a> 
<br><br><a href="https://datacake.co/">Datacake.co</a> 
<br><br><a href="https://pipedream.com/">PipeDream.com</a> 
<br><br><a href="https://www.scaleway.com/en/iot-hub/">Scaleway.com iot-hub:</a> 


<h6>Note: Most of these sites are fine, they just need a User Experienc Expert to make them faster to setup and easier to use. </h6>





<H3> The Old Index, before Adafruit won!</h3>


And here are links to the very important static webpages that can communicate with a few IOT cloud platforms<br>
<b>Webhooks are important for small WiFi capable devices to send data, receive an update would be a nice bonus<br>
  
<a href="ubidots05.html">ubidots05.html</a>   or <a href="ubidots06.html">ubidots06.html</a>   or <a href="adafruit01.html">adafruit01.html</a>      or <a href="ada02.html">ada02.html</a>  
  Webhook static webpage using http or  https that works with Ubidots and can send-only to Adafruit and probably many other integrations.
  Note: I think Ubidots may have tightened up some security because I can't seem to get it working again!<br><br>
 
 Websockets are important to be able to display data live on a webpage also a bonus if they can send data<br> 
<a href="hive05.html">hive05.html</a>  This SSL websocket static webpage can communicate with both mosquitto and HiveMQ and probably many more<br><br>
  
  
 <h3  align=center > Note: For HiveMQ more data needs to be entered: username, password, clientID<br>
 <img src="media/hive05.png"  width=600   style="border-style:groove; border-color:red; border-width:10px;  " /><br> </h3>
  
  
</b>
  








  
Why are static webpages important! NodeJS, PHP etc web servers are very powerful but confusing for my students. 
<a href="https://gitpod.io/">Gitpod</a> is amazing for development of complex web servers and it ties well with Github but does not stay active 
for very long, <a href="https://www.heroku.com/">Heroku Cloud</a> is also amazing and works well with Github but is also fairly complex for High School students. <br><br>

Many IOT cloud sites brag about their "Dashboard" and how easy it is to setup<br>
I like and dislike "dashboards" they are great to get things started, but eventually 
I will start to need the page view to be slightly different. Over the last 46 years of coding this situation has happened repeatedly where company programmers try to 
make things easier but actually just cripple your ability to do what you want.<br><br>

An example would be a machine learning program that works from your browser and updates an MQTT site every minute with summarized data. 
I could easily (might take a year LOL) make that however with Dashboards I would need my machine learning program on one webpage and the dashboard 
openned on another webpage. For many people that would be fine, for me that would be very frustrating.<br><br>

<h3>IOT cloud sites that are good but not yet great in my opinion!</h3>

<br><a href="">Arduino Cloud:</a> <br><font color="green">Great, easy to use, dashboard simple to understand</font><br> and has<font color="red"> NONE of my criteria in the image above! No free integrations, 
I have found no examples of easy to use MQTT, HTTPS, Websocket-MQTT. Even things like LoRaWan integrations with the 
  Portenta LoRa Vision Shield do not seem as easy to setup as they should be. </font>I love Arduino hardware and software, but the Arduino cloud is not yet ready 
for everything I need, other than<font color="green"> a quick student introduction to setting up a cloud variable and changing it.</font>




<br><br><a href="https://io.adafruit.com/">Adafruit:</a> <br><font color="green"> Free to use, great, easy to use MQTT connectivity also supports CayenneLPP simplification
of the JSON data, Dashboard easy to work with, works great with the <a href="https://explorer.helium.com/coverage">Helium</a> LoRaWan network and 
  the <a href="https://console.helium.com/">Helium Console</a>. Https webhooks works to update data</font>. <br> <font color="red">https Webhooks do not work to read data. Have not found websocket-MQTT</font>

<br><br><a href="https://tago.io/">TagioIO</a> or <a href="https://ubidots.com/">Ubidots</a> or <a href="https://thingspeak.com/">ThingSpeak:</a><br><font color="green"> Free to use. Great HTTPS connectivity, 
  my above webpage demonstrates Ubidots connectivity with https webhooks using a static webpage. </font><br><font color="red">I have not found websockets-MQTT and have not tested 
    arduino MQTT </font>

<br><br><a href="https://www.hivemq.com/">HiveMQ:</a><br><font color="green"> Free to use: Great WebSocket connectivity as seen by my static webpage above. 
  Fast easy to use and easy to understand. Lots of free use. </font><br><font color="red">Issues connecting websockets with Helium. I have not found out how to do https webhooks I have not yet tested MQTT with Arduino</font>


<br><br><a href="https://www.scaleway.com/en/iot-hub/">Scaleway iot-hub:</a> Was recommended to me but needs a Credit card
<br><br><a href="https://www.qubitro.com/">Qubitro:</a> Still testing, some positives some negatives

<br><br><a href="https://thingsboard.io/">Thingsboard:</a> Powerful, not sure if the cloud side is free.

<h3>A few resources I like</h3>


<li><a href="https://www.hivemq.com/blog/mqtt-over-websockets-with-hivemq/">hive mqtt</a><br>


<li><a href=https://io.adafruit.com/api/docs/?cpp#webhooks">adafruit</a><br>

<li><a href=https://help.ubidots.com/en/articles/3383755-connect-the-arduino-nano-33-iot-with-ubidots-over-http">Ubidots</a><br>


<h3>A few things I have done</h3>
  
Youtube video going through each of the IOT platforms I have found not really finding much <br>
  <iframe width="560" height="315" src="https://www.youtube.com/embed/MGS7GOdAExE" title="YouTube video player" frameborder="0" allow="accelerometer; autoplay; clipboard-write; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe>
  <br><br>
  
EdgeImpulse forum talking about the IOT cloud issue <a href="https://forum.edgeimpulse.com/t/arduino-mqtt-broker/2091/17">here</a>  <br><br>
  




The base github for this static webpage has a  README.md file with lots of Cloud IOT site links for twitter and webpages <a href="https://github.com/hpssjellis/my-examples-of-iot-platforms">here</a> <br><br>
  
The old research <a href="index-old.html"> index-old.html</a> page is here with lots of not working examples<br><br>
  
<h6>Opinions are my own. Use at your own risk:  By Jeremy Ellis Twitter <a href="https://twitter.com/rocksetta">@rocksetta</a> </h6>

  
<li><a href="..">...</a>
  
<li><a href="..">...</a>
  
<li><a href="..">...</a>
